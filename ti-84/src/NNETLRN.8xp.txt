ClrHome
dim(|LL)->N
Input "TRAINING SET=",[F]
dim([F])->|LF
If |LL(1)+|LL(N)!=|LF(2)
Then
	Disp "INVALID TRAINING SET"
	Return
End
Input "LEARNING RATE=",theta
Input "CYCLES:",E
For(X,1,E)
	ClrHome
	0->G
	randIntNoRep(1,|LF(1),|LF(1))->|LORDER
	For(U,1,|LF(1))
		Output(1,11,"CYCLE")
		Output(1,17,X)
		Output(1,23-int(log(U)),U)
		Output(1,24,"/")
		Output(1,25,|LF(1))
		|LORDER(U)->R
		seq(U,U,1,|LL(1))->|LI
		For(C,1,|LL(1))
			[F](R,C)->|LI(C)
		End
		Output(3,1,"                             ")
		Output(4,1,"                             ")
		Output(5,1,"                             ")
		Output(6,1,"                             ")
		Output(7,1,"                             ")
		Output(8,1,"                             ")
		Output(3,1,"RUNNING NETWORK ...")
		prgmNNET
		"BACKPROPAGATION
		{N-1,max(|LL)}->dim([H])
		Output(4,1,"COMPUTING INIT DELTAS ...")
		For(I,1,|LL(N))
			|LN(I)->Y
			[F](R,|LL(1)+I)->T
			(0.1+(1-Y^2))(T-Y)->[H](N-1,I)
			G+((T-Y)^2/|LL(N))->G
		End
		If N>2
		Then
			Output(5,1,"DETERMINING ERRORS ...")
			For(I,N-1,2,~1)
				For(J,1,|LL(I))
					[G](I,J)->Y
					0->Z
					For(K,1,|LL(I+1))
						Z+[H](I,K)*[I](I,(K-1)*|LL(I+1)+J)->Z
					End
					(1-Y^2)*Z->[H](I-1,J)
				End
			End
			Output(6,1,"UPDATING NODES ...")
			Output(8,1,"                  ")
			For(I,2,N)
				For(J,1,|LL(I))
					For(K,1,|LL(I-1))
						[G](I-1,K)*[H](I-1,J)->D
						[I](I-1,(J-1)*|LL(I-1)+K)->W
						W+theta*D->[I](I-1,(J-1)*|LL(I-1)+K)
						Output(7,1,"                    ")
						Output(7,1,"L")
						Output(7,2,I-1)
						Output(7,3,":")
						Output(7,5,K)
						Output(7,7,"=>")
						Output(7,10,J)
					End
				End
			End
			Output(7,1,"UPDATING BIASES ...")
			For(I,2,N)
				For(J,1,|LL(I))
					[H](I-1,J)->D
					[J](I-1,J)->W
					W+theta*D->[J](I-1,J)
					Output(8,1,"L")
					Output(8,2,I-1)
					Output(8,3,": B =>")
					Output(8,10,J)
				End
			End
		End
	End
	Output(8,1,"                       ")
	Output(9,1,"ERROR:")
	Output(9,8,round(G/|LF(1),6))
	G/|LF(1)->|LERR(dim(|LERR)+1)
	If X<E
	Then
		If dim(rand(300))
	End
End
Pause
ClrHome
